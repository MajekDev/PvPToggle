plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.linguica.maven-settings' version '0.5'
    id 'net.kyori.indra' version '2.0.6'
    id 'net.kyori.indra.license-header' version '2.0.6'
    id 'net.kyori.indra.publishing' version '2.0.6'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    //id("net.kyori.indra.checkstyle") version indraVersion
}

group = 'dev.majek'
version = '3.0.0-SNAPSHOT'
description = 'A simple plugin that allows players to easily toggle their PvP preference.'

repositories {
    mavenCentral()
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    implementation('net.kyori:adventure-api:4.9.2')
    implementation('net.kyori:adventure-text-minimessage:4.2.0-SNAPSHOT')
    implementation('net.kyori:adventure-platform-bukkit:4.0.0')
    implementation('org.jetbrains:annotations:22.0.0')
    implementation('com.google.code.gson:gson:2.8.8')
    implementation('com.tchristofferson:ConfigUpdater:2.0-SNAPSHOT')
    implementation('org.bstats:bstats-bukkit:2.2.1')
    compileOnly('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT')
    compileOnly('com.github.angeschossen:LandsAPI:5.15.2')
    compileOnly('me.clip:placeholderapi:2.10.10')
    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.0.6')
    compileOnly('com.github.TechFortress:GriefPrevention:16.17.1')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.1')
}

publishing {
    repositories {
        maven {
            name = "majek-snapshots"
            url = "https://repo.majek.dev/snapshots"
        }
        maven {
            name = "majek-repo"
            url = "https://repo.majek.dev/releases"
        }
    }
}

jar {
    archiveClassifier.set("original")
    archiveBaseName.set("PvPToggle")
}

sourcesJar {
    archiveBaseName.set("PvPToggle")
}

javadocJar {
    archiveBaseName.set("PvPToggle")
}

artifacts {
    archives shadowJar
}

shadowJar {
    dependencies {
        relocate('org.jetbrains', 'dev.majek.relocations.org.jetbrains')
        relocate('net.kyori', 'dev.majek.relocations.net.kyori')
        relocate('org.intellij', 'dev.majek.relocations.org.intellij')
        relocate('com.google.gson', 'dev.majek.relocations.com.google.gson')
        relocate('com.tchristofferson', 'dev.majek.relocations.com.tchristofferson')
        relocate('org.bstats', 'dev.majek.relocations.org.bstats')
    }
    archiveClassifier.set("")
    archiveBaseName.set("PvPToggle")
}

indra {
    javaVersions {
        testWith(8, 11, 16)
    }

    github('Majekdor', 'PvPToggle') {
        ci(true)
    }
    mitLicense()
}